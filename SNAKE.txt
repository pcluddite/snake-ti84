AxesOff
ZStandard
ClrDraw
RecallPic Pic0
For(A,1,500):End
ClrHome
{8.4,8.3,8.2}→ʟS
3→S
0→I
26→D
0→M
8.4→ʟS(1)
1→C
AxesOn
While 1
S→dim(ʟS)
getKey→K
If K=24 or K=25 or K=26 or K=34:K→D
If K=105:Then
AxesOff
Text(33,28,"SCORE:"
Text(33,52,M)
Text(25,28,"GAME PAUSED"
Pause 
Output(10fPart(ʟS(1)),int(ʟS(1)),"H"
Pause 
Output(10fPart(ʟS(1)),int(ʟS(1)),"O"
End

augment({0},ʟS→ʟS
ʟS(2)+(D=26)-(D=24)+.1((D=34)-(D=25))→ʟS(1)
Ans+16(not(int(Ans))-(17=int(Ans)))+.8(not(fPart(Ans))-(.9=fPart(Ans
Ans→ʟS(1
int(ʟS(1))→X
10fPart(ʟS(1))→Y
ʟS(1→A
9→ʟS(1
If max(ʟS=A
Then
Lbl 0
Output(10fPart(A),int(A),"X")
ClrDraw
AxesOff
Text(20,32,"YOU DIED!"
Horizontal ⁻2
Horizontal 5
Text(27,32,"SCORE:"
Text(27,55,M
Pause 
Goto H
Else
A→ʟS(1)
Output(10fPart(A),int(A),"O"
If ʟS(S+1)≠I:Output(10fPart(ʟS(S+1)),int(ʟS(S+1))," "
If I=ʟS(1):Then:5+M→M
If S≠39:S+1→S
0→I
End
End
If C≥30 and I=0:Then
1→C
randInt(1,16)→I
(randInt(1,8)/10)+I→I
Output(10fPart(I),int(I),"*"
Else:C+1→C:End
End
Lbl H
ClrDraw
5→dim(ʟSNHS)
For(A,1,5)
If ʟSNHS(A)=0:0.0101→ʟSNHS(A)
End
augment(ʟSNHS,{M})→ʟSNHS
SortD(ʟSNHS)
5→dim(ʟSNHS)
DelVar I
DelVar D
DelVar K
DelVar Str0
DelVar A
DelVar ʟS
Text(10,27,"HIGH SCORES"
0→C
For(A,1,5
int(ʟSNHS(A)→H
int(100fPart(ʟSNHS(A))→I
100fPart(100fPart(ʟSNHS(A)))→J
If A≠1:If H≠(int(ʟSNHS(A-1)):C+1→C
Text(A*7+12,15,C
Text(A*7+12,20,")"
Text(A*7+12,25,H
{I,J}→ʟTEMP
"?
For(B,1,2
Ans→Str0
If ʟTEMP(B)=0:1→ʟTEMP(B)
Str0
Ans+sub("ABCDEFGHIJKLMNOPQRSTUVWXYZ",ʟTEMP(B),1
End
sub(Ans,2,length(Ans)-1→Str0
Text(A*7+12,65,Str0
If H=M and fPart(ʟSNHS(A))=0:Then
Text(A*7+12,65,"YOU"
⁻1→S
End
End
Pause 
If S=⁻1:Then
Lbl IN
ClrHome
Disp "YOU GOT A"
Disp "HIGH SCORE!"
Input "ENTER INITIALS: ",Str0
If length(Str0)≠2:Then
Disp "INVALID"
Pause 
Goto IN
Else
DelVar ʟTEMP
For(A,1,2
inString("ABCDEFGHIJKLMNOPQRSTUVWXYZ",sub(Str0,A,1→ʟTEMP(A
End
For(A,1,5
If fPart(ʟSNHS(A))=0:Then
ʟTEMP(1)/100+ʟSNHS(A)→ʟSNHS(A)
ʟTEMP(2)/10000+ʟSNHS(A)→ʟSNHS(A)
End
End
End
End
DelVar ʟTEMP
DelVar S
DelVar I
DelVar J
DelVar C
DelVar Str0
DelVar A
DelVar B
AxesOn
Output(1,1,"")
